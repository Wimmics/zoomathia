PREFIX oa:     <http://www.w3.org/ns/oa#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT ?paragraph ?name_animal ?mention_animal ?name_use ?mention_use WHERE {

  ?annotation2 oa:hasBody ?use;
              oa:hasTarget [oa:hasSource ?paragraph;
                            oa:hasSelector [oa:exact ?mention_use]].
                            
  ?use skos:prefLabel ?name_use;
	  skos:broader* <https://opentheso.huma-num.fr/?idc=105582&idt=th310>.

  ?annotation1 oa:hasBody ?animal;
              oa:hasTarget [oa:hasSource ?paragraph;
                            oa:hasSelector [oa:exact ?mention_animal]].
  ?animal a skos:Concept;
       skos:prefLabel ?name_animal.
  <https://opentheso.huma-num.fr/?idg=MT_7&idt=th310> skos:member ?animal.
  
  FILTER (lang(?name_animal) = "en")
  FILTER (lang(?name_use) = "en")
}
ORDER BY ?paragraph