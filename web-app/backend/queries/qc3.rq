PREFIX oa:     <http://www.w3.org/ns/oa#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema:  <http://schema.org/>

SELECT DISTINCT ?paragraph ?name_animal ?mention1 ?mention2 WHERE {
  ?annotation1 a oa:Annotation;
              oa:hasBody ?animal;
              oa:hasTarget [oa:hasSource ?paragraph;
                            oa:hasSelector [oa:exact ?mention1]].
  ?animal a skos:Concept;
       skos:prefLabel ?name_animal.
  ?animal_collection a skos:Collection;
       skos:prefLabel "Zoonyms"@en;
       skos:member ?animal.

  ?annotation2 a oa:Annotation;
              oa:hasBody ?use;
              oa:hasTarget [oa:hasSource ?paragraph;
                            oa:hasSelector [oa:exact ?mention2]].
  ?use skos:prefLabel ?name_use;
	skos:broader+ ?use_generique.
  ?use_generique skos:prefLabel "medical use of animal"@en.  
  
  FILTER (lang(?name_animal) = "en")
  FILTER (lang(?name_use) = "en")
}
ORDER BY ?paragraph